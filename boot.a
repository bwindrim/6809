		org	$F000
		ldx	#$E00
loop1:		lda	$EFFF,x
		sta	$EFFF,x
		leax	-1,x
		bne	loop1
		ldx	#256
loop2:		lda	$FEFF,x
		sta	$FEFF,x
		leax	-1,x
		bne	loop2
		lda	#$FE	; point the DP reg to the I/O register space
		exg	a,dp
		lda	#0		; set DDRA to input
		sta	<3		; $FE03 is DDRA
		lda	#$FF	; set DDRB to ouput
		sta	<2		; $FE02 is DDRB
		lda	#1
		sta	<$0B	; $FE0B is ACR
		lda	#$AA
		sta	<$0C	; $FE0C is PCR
loop3:		ldu	#getbyte
		exg	u,pc
		cmpb	#$AA
		bne	loop3
dload:		ldu	#getword
		exg	u,pc
		exg	d,x
		ldu	#getword
		exg	u,pc
		exg	d,y
		leay	1,y
loop4:		leay	-1,y
		beq	exit
		ldu	#getbyte
		exg	u,pc
		stb	,x+
		bra	loop4
exit:		ldu	#getword
		exg	u,pc
		exg	d,pc
int:		rti
getword:	lda	<$0D
		anda	#2
		beq	getword
		lda	<1
		sta	<0
getbyte:	ldb	<$0D
		andb	#2
		beq	getbyte
		ldb	<1
		stb	<0
		exg	u,pc
