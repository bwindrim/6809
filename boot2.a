; boot2.a - second-stage bootloader for 6809 board
;
			include			"via6522.i"
			
			org	$FF00   	; boot2's code+stack goes in last 256 bytes of RAM

			lds	#$FFF0  	; stack grows downward from the very top of RAM, below the hardware vectors

			lda	#0			; set DDRA to input
			sta	ddra		; $FE03 is the 6522's DDRA (Port A Data Direction Register)
			lda	#$FF		; set DDRB to output
			sta	ddrb		; set Port B Data Direction Register
			lda	#1			; set ACR to 00000001 Timed interrupt, PB7 disabled, timed interrupt, shifter disabled, PB latch disable, PA latch enable
			sta	acr			; set Auxiliary Control Register
			lda	#$AA		; set PCR to 10101010 CB2 pulse output, CB1 -ve edge, CA2 pulse ouput, CA1 -ve edge
			sta	pcr			; set Program Control Register
restart:
wait:		bsr getbyte		; wait for the download prefix byte
			cmpb #$AA
			bne	wait

dload:		bsr	getword		; get the load address
			exg	d,x			; move it to X
			bsr	getword		; get the length
			exg	d,y			; move it to Y
			leay	1,y		; increment the length to allow for the initial decrement & test (length may be zero)
byteloop:	leay	-1,y	; decrement the length count
			beq	exit		; if Y is zero then we're done reading bytes
			bsr	getbyte		; get the next byte
			stb	,x+			; store it in memory
			bra	byteloop

exit:		ldx	#restart	; restart address of boot2 code
			pshs X			; push the address onto the stack
			bsr	getword		; get the exec address
			exg	d,pc		; transfer execution to the exec address

getword:	lda	ifr			; load A from IFR
			anda #2			; test CA1 (Port A Data Ready) flag
			beq	getword		; loop if no data ready

			lda	porta		; load A from port A input register
			sta	portb		; store A to port B output register (blind echo, no handshaking)
			; fall through
getbyte:	ldb	ifr			; load A from IFR
			andb	#2		; test CA1 (Port A Data Ready) flag
			beq	getbyte		; loop if no data ready

			ldb	porta		; load B from port A input register
			stb portb		; store B to port B pitput register (blind echo, no handshaking)
			rts				; return from subroutine
